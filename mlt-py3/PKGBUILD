# $Id: PKGBUILD 291218 2017-03-22 19:50:10Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>

pkgbase=mlt
pkgname=(mlt mlt-python3-bindings)
pkgver=6.4.1
pkgrel=1
pkgdesc="An open source multimedia framework"
arch=(i686 x86_64)
url="http://www.mltframework.org"
license=(GPL)
makedepends=(ladspa frei0r-plugins libdv sdl_image libsamplerate sox ffmpeg vid.stab qt5-svg jack libexif python2 swig movit eigen)
source=("https://github.com/mltframework/mlt/archive/v$pkgver.tar.gz")
md5sums=('e9f0584739369c40b8f91f8cf50ea3ea')

prepare() {
  cd "$srcdir/mlt-$pkgver"
  # Patch mlt.i for python3
  patch -p1 -i ../../swigpython3.patch
}

build() {
  # mlt
  cd mlt-$pkgver

  msg "SSE2=$SSE2"
  ./configure --prefix=/usr \
    --avformat-swscale \
    --enable-gpl --enable-gpl3 \
    --qt-libdir=/usr/lib --qt-includedir=/usr/include/qt
  make

  # mlt python bindings
  cd src/swig/python
  sed -i 's#export PYTHON_INCLUDE.*$#export PYTHON_INCLUDE=/usr/include/python3.6m#' build
  sed -i 's#swig -c++#swig -py3 -c++#' build
  ./build
}

package_mlt() {
  optdepends=('sdl_image: for SDL plugin'
    'libsamplerate: for libavresample plugin'
    'sox: for SOX (Audio Swiss Army Knife) plugin'
    'ffmpeg: for ffmpeg plugin'
    'vid.stab: for video stabilize plugin'
    'gtk2: Gtk plugin'
    'qt5-svg: for Qt5 plugins'
    'jack: for JACK sound output plugin'
        'ladspa: for LADSPA plugins'
    'libexif: for auto rotate plugin'
    'frei0r-plugins: for additional effects'
    'movit: opengl plugin')
  conflicts=('mlt++<=0.3.8')

  cd mlt-$pkgver
  make DESTDIR="$pkgdir" install
}

package_mlt-python3-bindings() {
  depends=(python mlt)

  cd "$srcdir/mlt-$pkgver/src/swig/python"
  mkdir -p "$pkgdir/usr/lib/python3.6/"
  install -m755 mlt.py "$pkgdir/usr/lib/python3.6/"
  install -m755 _mlt.so "$pkgdir/usr/lib/python3.6/"
  install -m755 mlt_wrap.o "$pkgdir/usr/lib/python3.6/"
}
